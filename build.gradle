import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {

    ext.isTravis = System.getenv("TRAVIS") == "true"
    ext.travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
    ext.isReleaseVersion = !isTravis

    ext.kotlin_version = "1.4.10"
    ext.kotest_version = "4.2.3"

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'io.kotest' version '0.1.3'
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = "com.sksamuel.hoplite"

    if (isTravis) {
        version "1.3.7." + travisBuildNumber + "-SNAPSHOT"
    } else {
        version "1.3.6"
    }

    tasks.withType(Javadoc) {
        onlyIf { isReleaseVersion }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
        testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotest_version"
    }

    test {
        useJUnitPlatform()
        testLogging {
            showExceptions = true
            showStandardStreams = true
            events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

project(":") {
    publish.enabled = false
}
